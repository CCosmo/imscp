#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2013 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
#
# @category		i-MSCP
# @copyright	2010-2013 by i-MSCP | http://i-mscp.net
# @author		Daniel Andreca <sci2tech@gmail.com>
# @link			http://i-mscp.net i-MSCP Home Site
# @license		http://www.gnu.org/licenses/gpl-2.0.html GPL v2

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/..";
use lib "$FindBin::Bin/../PerlLib";

use iMSCP::Debug;
use iMSCP::Boot;
use iMSCP::Mail;
use iMSCP::Servers;

# Turn off localisation features to force any command output to be in english
$ENV{'LC_MESSAGES'} = 'C';

newDebug('imscp-vrl-traff.log');

silent(1);

sub traff_start_up
{
	umask(027);

	iMSCP::Boot->getInstance()->boot({ 'nolock' => 'yes', 'config_readonly' => 'yes' });

	@{$main::Servers} = iMSCP::Servers->getInstance()->get();

	0;
}

sub traff_count
{
	my $rs = 0;
	my $timestamp = time;
	my %traff;

	# timestamp has floor down for the last half'n our to make timestamps in
	# server_traffic and domain_traffic the same
	$timestamp = $timestamp - ($timestamp % 1800);

	my $sql = "
		SELECT `domain_id`, `domain_name`, 'domain' AS `type` FROM `domain`
		UNION
		SELECT `domain_id`, `alias_name`, 'domain_aliasses' AS `type` FROM `domain_aliasses`
		UNION
		SELECT `domain_id`, CONCAT(`subdomain_name`, '.', `domain_name`), 'subdomain' AS `type` FROM `subdomain` LEFT JOIN `domain` USING (`domain_id`)
		UNION
		SELECT `domain_id`, CONCAT(`subdomain_alias_name`, '.', `alias_name`), 'subdomain_alias' AS `type` FROM `subdomain_alias` LEFT JOIN `domain_aliasses` USING (`alias_id`)
	";
	my $rdata = iMSCP::Database->new()->factory()->doQuery('domain_name', $sql);

	if (ref $rdata ne 'HASH'){
		error($rdata);
		iMSCP::Mail->new()->errmsg($rdata);
		$rs = 1;
	} else {

		for (keys %$rdata) {

			my $domain_name = $rdata->{$_}->{'domain_name'};
			my $domain_id = $rdata->{$_}->{'domain_id'};
			my $type = $rdata->{$_}->{'type'};
			my $traffic = 0;

			for(@{$main::Servers}) {
				s/\.pm//;
				my $file = "Servers/$_.pm";
				my $class = "Servers::$_";
				require $file;
				my $instance = $class->factory();
				if($instance->can('getTraffic')) {
					debug("Calling the $_ server getTraffic() method for $domain_name");
					$traff{$domain_id}->{$_} += $instance->getTraffic($domain_name);
				} else {
					debug("i-MSCP $_ server doesn't implement the getTraffic() method");
				}

				debug("Traffic for $type $domain_name for $_ is $traff{$domain_id}->{$_}") if exists $traff{$domain_id}->{$_};
			}
		}
	}

	for(keys %traff) {
		next unless $traff{$_}->{'httpd'} || $traff{$_}->{'ftpd'} || $traff{$_}->{'mta'} || $traff{$_}->{'po'};

		$sql = "
			SELECT
				`dtraff_id`
			FROM
				`domain_traffic`
			WHERE
				`domain_id` = ?
			AND
				`dtraff_time` >= UNIX_TIMESTAMP(CURRENT_DATE())
			LIMIT 1
		";
		my $result = iMSCP::Database->new()->factory()->doQuery('dtraff_id', $sql, $_);

		if (ref $result ne 'HASH'){
			error($result);
			iMSCP::Mail->new()->errmsg($result);
			$rs = 1;
		} else {
			unless (scalar keys %{$result}) {
				$sql = "
					INSERT INTO
						`domain_traffic`
					(
						`domain_id`, `dtraff_time`, `dtraff_web`, `dtraff_ftp`, `dtraff_mail`, `dtraff_pop`
					) VALUES (
						?, ?, ?, ?, ?, ?
					)
				";
				$result = iMSCP::Database->new()->factory()->doQuery(
					'update', $sql, $_, $timestamp,
					exists $traff{$_}->{'httpd'} ? $traff{$_}->{'httpd'} : 0,
					exists $traff{$_}->{'ftpd'} ? $traff{$_}->{'ftpd'} : 0,
					exists $traff{$_}->{'mta'} ? $traff{$_}->{'mta'} : 0,
					exists $traff{$_}->{'po'} ? $traff{$_}->{'po'} : 0
				);
				if (ref $result ne 'HASH'){
					error($result);
					iMSCP::Mail->new()->errmsg($result);
					$rs = 1;
				}
			} else {
				$sql = "
					UPDATE
						`domain_traffic`
					SET
						`dtraff_web` = `dtraff_web` + ?, `dtraff_ftp` = `dtraff_ftp` + ?,
						`dtraff_mail` = `dtraff_mail` + ?, `dtraff_pop` = `dtraff_pop` + ?, `dtraff_time` = ?
					WHERE
						`domain_id` = ?
					AND
						`dtraff_id` = ?
				";
				$result = iMSCP::Database->new()->factory()->doQuery(
					'update', $sql,
					exists $traff{$_}->{'httpd'} ? $traff{$_}->{'httpd'} : 0,
					exists $traff{$_}->{'ftpd'} ? $traff{$_}->{'ftpd'} : 0,
					exists $traff{$_}->{'mta'} ? $traff{$_}->{'mta'} : 0,
					exists $traff{$_}->{'po'} ? $traff{$_}->{'po'} : 0,
					$timestamp, $_, (keys %{$result})[0]
				);
				if (ref $result ne 'HASH'){
					error($result);
					iMSCP::Mail->new()->errmsg($result);
					$rs = 1;
				}
			}
		}
	}

	$rs;
}

my $rs = 0;

exit 1 if traff_start_up();
exit traff_count();
